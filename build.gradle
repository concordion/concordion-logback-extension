apply from: 'https://raw.githubusercontent.com/concordion/concordion-extension-build/master/extension-build.gradle'
// apply from: 'extension-build.gradle'

description = 'An extension to Concordion that adds Logback logging support'

/*
repositories {
	mavenLocal()
}
*/

ext {
	developers = {
		developer {
			id 'andrew-sumner'
			name 'Andrew Sumner'
			roles { role 'Project owner' }
		}
	}
	inceptionYear = '2014'
}


sourceSets {
    reportlogger
}

dependencies {
	reportloggerImplementation 'org.slf4j:slf4j-ext:1.7.31'
    reportloggerImplementation 'ch.qos.logback:logback-classic:1.2.3'
    reportloggerImplementation 'commons-io:commons-io:2.10.0'
        
    api sourceSets.reportlogger.output
    api sourceSets.reportlogger.compileClasspath
    
	api "org.concordion:concordion-logging-tooltip-extension:1.1.2"
		
	// Only needed for javadocs :-(
	reportloggerCompileOnly 'commons-lang:commons-lang:2.6'
}

// build reportlogger jar
task reportloggerJar(type: Jar) {
    from sourceSets.reportlogger.output
}

// TODO add equivalent for Gradle 7
// // Include reportlogger source set in sources jar
// sourcesJar  { 
//     from sourceSets.reportlogger.allSource
// } 

publishing { 
	publications { 
        mavenJava(MavenPublication) { 
            artifact reportloggerJar { 
            	classifier 'reportlogger' 
            }
		}
	}
}

test.dependsOn cleanTest
test {
    exclude 'specification/BaseFixture.class'
	systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
	outputs.upToDateWhen { false } // force it to run even if test code hasn't changed
	testLogging {
		events "passed", "skipped", "failed", "standardError"
	}
}
